*** Settings ***
Resource    ResultPageKeywords.txt
Library     String

*** Variables ***
${TOZIPCODEFIELD}      id=HomeForm_fldTo
${FROMZIPCODEFIELD}    id=HomeForm_fldFrom

${ZIPCODESELECTOR}    xpath=//a[contains(text(),"zipcode")]
${ZIPCODETOSELECTOR}    xpath=//*[@id=\"ui-id-10\"]//a[contains(text(),"zipcode")]

*** Keywords ***
Enter parcel weight    [Arguments]     ${parcelNumber}       ${weight}
	Input Text     id=ParcelForm_${parcelNumber}_weight     ${weight}

Enter parcel length    [Arguments]     ${parcelNumber}       ${length}
	Input Text     id=ParcelForm_${parcelNumber}_length     ${length}

Enter parcel width    [Arguments]     ${parcelNumber}       ${width}
	Input Text     id=ParcelForm_${parcelNumber}_width     ${width}

Enter parcel height   [Arguments]     ${parcelNumber}      ${height}
	Input Text     id=ParcelForm_${parcelNumber}_height    ${height}

Click Search Button
	Click Element   link=Ricerca

Enter From Zipcode    [Arguments]    ${fromZipCode}
	Input Text    ${FROMZIPCODEFIELD}    ${fromZipCode}
	${resultLink}=    Replace String    ${ZIPCODESELECTOR}    zipcode    ${fromZipCode}
	Wait Until Page Contains Element    ${resultLink}
	Wait Until Element Is Visible    ${resultLink}
	Click Element    ${resultLink}

Enter To Zipcode    [Arguments]      ${toZipCode}
	Input Text    ${TOZIPCODEFIELD}    ${toZipCode}
	${resultLink}=    Replace String    ${ZIPCODETOSELECTOR}    zipcode    ${toZipCode}
	Wait Until Page Contains Element    ${resultLink}
	Wait Until Element Is Visible    ${resultLink}
	Click Element    ${resultLink}

Search Is Successful
	Wait Until Element Is Visible    ${RESULTS}  
	Title Should Be    Scegli servizio

Search Is Unsuccessful
	Wait Until Element Is Visible    ${SEARCHBUTTON}

Search Should Pass When    [Arguments]    ${weight}    ${length}    ${width}    ${height}    ${from}    ${to}
	Search Shipments With    ${weight}    ${length}    ${width}    ${height}    ${from}    ${to}
	Search Is Successful

Search Should Fail When    [Arguments]    ${weight}    ${length}    ${width}    ${height}    ${from}    ${to}
	Search Shipments With    ${weight}    ${length}    ${width}    ${height}    ${from}    ${to}
	Search Is Unsuccessful

Search Shipments With    [Arguments]    ${weight}    ${length}    ${width}    ${height}    ${from}    ${to}
	Enter From Zipcode     ${from}
	Enter To Zipcode       ${to}
	Enter parcel weight    0        ${weight}
	Enter parcel length    0        ${length}
	Enter parcel width     0        ${width}
	Enter parcel height    0        ${height}
	Click Search Button



